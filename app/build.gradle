apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'kotlin-parcelize'

android {
    compileSdkVersion 29
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.githubusers.android"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "0.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }

    signingConfigs {
        release {
            storeFile file('../keystore/githubusers.keystore')
            storePassword "keystore password here"
            keyAlias 'keystore alias here'
            keyPassword "keystore alias password here"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // Create host value for server dev, staging, and production
    def devApiHost = "https://api.github.com/"
    def prodApiHost = "https://api.github.com/"

    // Default flavor dimension value if no set specific dimensions for each flavor
    flavorDimensions "version"

    productFlavors {
        dev {
            dimension "version"
            applicationIdSuffix ".dev"
            versionCode 1
            versionName "0.0.1"
            buildConfigField "String", "API_HOST", "\"${devApiHost}\""
        }
        prod {
            dimension "version"
            versionCode 1
            versionName "0.0.1"
            buildConfigField "String", "API_HOST", "\"${prodApiHost}\""
        }
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

// Rename output apk e.g. githubusers_dev_debug_1.0.0_1.apk
android.applicationVariants.all { variant ->
    variant.outputs.all { output ->
        def baseName = "githubusers"
        def productFlavor = variant.productFlavors[0].name
        def builtType = variant.buildType.name
        def versionName = variant.versionName
        def versionCode = variant.versionCode
        outputFileName = "${baseName}_${productFlavor}_${builtType}_${versionName}_${versionCode}.apk"
    }
}

kapt {
    correctErrorTypes true
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.security:security-crypto:1.0.0-rc01'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    def hilt_version = "2.28-alpha"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    def hilt_viewmodels = "1.0.0-alpha01"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$hilt_viewmodels"
    kapt "androidx.hilt:hilt-compiler:$hilt_viewmodels"

    def nav_version = "2.3.2"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    def fragment_ktx = "1.2.5"
    implementation "androidx.fragment:fragment-ktx:$fragment_ktx"

    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"

    def work_runtime_version = "2.3.4"
    implementation "androidx.work:work-runtime:$work_runtime_version"

    // OkHttp
    def okhttp_version = "4.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.squareup.okhttp3:okhttp:4.9.0"

    // Retrofit
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    // Room
    def room = "2.2.5"
    implementation "androidx.room:room-runtime:$room"
    implementation "androidx.room:room-ktx:$room"
    kapt "androidx.room:room-compiler:$room"

    // Timber
    def timber_version = "4.7.1"
    implementation "com.jakewharton.timber:timber:$timber_version"

    // Material Design
    def material_version = "1.3.0"
    implementation "com.google.android.material:material:$material_version"

    // Card View
    def cardview_version = "1.0.0"
    implementation "androidx.cardview:cardview:$cardview_version"

    // Recyclerview
    def recyclerview_version = "1.1.0"
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"

    // Glide
    def glide_version = "4.11.0"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // SwipeRefreshLayout (reference for CircularProgressDrawable)
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    // Facebook Shimmer
    def shimmer_version = "0.1"
    implementation "com.facebook.shimmer:shimmer:$shimmer_version.0@aar"
}
